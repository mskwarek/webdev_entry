# -*- mode: Python; -*- scons script

import os, platform, shutil

Import('env')

def copyLibBuilder( target, source, env):
   '''kopiuje biblioteke'''
   shutil.copy( str(source[0]), str(target[0]) )
   return

#sciezki
env.Append( ENV = {'PATH' : os.environ['PATH'] })

if(platform.system() == "Linux"):
   env.Append( CPPPATH = ['/usr/include/python2.7'] )
   env.Append( LIBPATH = ['/usr/lib/python2.7'] )

   env.Append( CPPFLAGS = '-Wall -std=c++11 -pedantic -pthread' )
   env.Append( LINKFLAGS = '-Wall -pthread' )

   env.Append( LIBS = [ 'boost_python' ] )
elif(platform.system() == "Windows"):
   env.Append( CPPPATH = [ Dir('C:/Boost/include/boost-1_52'), #path to boost include
                           Dir('C:/Python27/include') ] ) #path to python include
   env.Append( LIBPATH = [ Dir('C:/Boost/lib'), #path to boost libs
                           Dir('C:/Python27/libs') ] ) #path to python libs

   env.Append( CPPFLAGS = ' /EHsc /MD /D "WIN32" /D "_CONSOLE" /W4' )
   env.Append( LINKFLAGS = ' /SUBSYSTEM:WINDOWS ' )
else:
   print platform.system() + " not supported"

#build C++ library
cpplib = env.SharedLibrary( target = 'AtBash', source = ['src/AtBash.cpp', 'src/AtBashCode.cpp'])
if(platform.system() == "Linux"):
   target = '../build_web/AtBashCoderPython/AtBash.so'
elif(platform.system() == "Windows"):
   target = '../build_web/AtBashCoderPython/AtBash.pyd'
env.Command(target, cpplib, copyLibBuilder )
